# Maintainer: Pierre Schmitz <pierre@archlinux.de>
pkgname=zlib
epoch=1
pkgver=1.2.11
pkgrel=4
arch=('x86_64')
license=('custom')
url="https://www.zlib.net/"
depends=('musl')
options=('staticlibs')  # needed by binutils testsuite
source=("https://zlib.net/zlib-${pkgver}.tar.gz.asc"
		"https://zlib.net/zlib-${pkgver}.tar.gz")
sha256sums=('aeee5d17282da556bb399e16d894ebbf22a59138fc6ca96e96614f62e1b5d9d0'
         	'SKIP')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')

prepare() {
	cd ${srcdir}/zlib-$pkgver
	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
	cd ${srcdir}/zlib-$pkgver
	./configure --prefix=/usr
	make

	cd contrib/minizip
	cp Makefile Makefile.orig
	cp ../README.contrib readme.txt
	autoreconf --install
	./configure --prefix=/usr --enable-static=no
	make
}

check() {
	cd ${srcdir}/zlib-$pkgver
	make test

	cd contrib/minizip
	make -f Makefile.orig test
}

package() {
	pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir}
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}
